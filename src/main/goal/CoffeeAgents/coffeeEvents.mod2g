%%%%% Event module shared by agents that are part of the coffee.mas2g multi-agent system. %%%%%
use coffee as knowledge.

module coffeeEvents {
	%%%%% Process received messages %%%%%
	
		% Ask all other machines what they can provide. 
	% we insert haveProviderInfo right after sending because reply is not obligatory.
	if bel(not(haveProviderInfo)) then allother.send?(canProvide(_,_) ) + insert(haveProviderInfo).
	
	% Believe what other machines say they can make (if multiple answers are provided we store all of them).
	forall (Sender).sent(canProvide(Sender, Products)) do insert( canProvide(Sender, Products) ) .
	% Believe what other machines say they have and can provide (telling and providing is taken to be the same here).
	forall (Sender).sent(have(Product)) do insert( have(Product) ) .


	%%%%% Goal management %%%%%
	
	% If a machine told us it needs an ingredient and we can make it, then adopt a goal to make it.
	forall (Sender).sent!(have(Ingredient)), bel(me(Me), canProvideIt(Me, Ingredient) )
		do adopt( have(Ingredient) ) .
	% If we need an ingredient for making a product, adopt a goal to have it (only if we do not have it yet).
	forall goal( have(Product) ), bel( requiredFor(Product, Ingredient) ) do adopt( have(Ingredient) ) .


	%%%%% Send messages to other machines %%%%%
	

	% Remember questions about what we can make
	forall (Sender1).sent?(canProvide(Sender,Product)), bel(me(Me), canProvide(Me, Products) ) 
		do adopt(informed(Sender,Product)).
	
	% Answer questions about what we can make.
	forall a-goal(informed(Sender,Product)), bel(me(Me), canProvide(Me, Product) )
		do (Sender).send(canProvide(Me, Product) ) + insert(informed(Sender,Product)).
		
	% If a machine told us it needs an ingredient and we have it, tell it so
	forall (Sender).sent?(have(Ingredient)), bel( have(Ingredient) ) 
		do (Sender).send(have(Ingredient)).
	% If we want to have a product we cannot make ourselves, then ask for it.
	forall goal( have(Product) ) do {
		% If it's a raw product and we know a machine that can provide it, ask that machine.
		if bel( rawProduct(Product), canProvideIt(Machine, Product) ) then (Machine).send!(have(Product) ) .
		% Also ask for help of another machine that can provide an ingredient that we cannot provide ourselves.
		if bel( requiredFor(Product, Ingredient), me(Me), not(canProvideIt(Me, Ingredient)),
				canProvideIt(Machine, Ingredient) )
			then (Machine).send!(have(Ingredient)) .
	}
		
	% Questions and requests cannot always be handled immediately, as we may need time to collect information
	% to answer questions and to process requests, we do not delete these types of messages.
	% FIXME
	

}
