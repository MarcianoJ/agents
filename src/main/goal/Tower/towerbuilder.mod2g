use tower as knowledge.
use towerGoal as goals.
use tower as actionspec.

% a block is misplaced if it is not in position.
%define misplaced(X) as a-goal( tower([X|T]) ).
% a block X is an obstructing block if it prevents moving a block Y in position
% either because there is a block above the target block,
% or because a block above Y prevents moving it.
define obstructingBlock(X) as a-goal( on(Y,Z) ), bel( above(X,Z); above(X,Y) ).
% moving X on top of Y is a constructive move if that move results in X being in position.
define constructiveMove(X,Y) as a-goal( tower([X,Y|T]) ), bel( tower([Y|T]), clear(Y), (clear(X) ; holding(X)) ).
% a block is *in position* if it achieves a goal.
define inPosition(X) as goal-a( tower([X|T]) ). 
	
module main {
	% PART 1: Think before you act...
	
	% if holding block without any target, the agent should put it on the table.
	if not(goal( target(Z1, Z2) )), bel( holding(X) ) then adopt( target(X, table) ).
	
	% set target, perform constructive moves first.
	if not(goal( target(Z1, Z2) )), constructiveMove(X, Y) then adopt( target(X, Y) ).
	% move obstructing (including misplaced) blocks to the table.
	if not(goal( target(Z1, Z2) )), obstructingBlock(X), bel( clear(X) ) then adopt( target(X, table) ).
	
	% if goal has been achieved, then drop it.
	if goal( target(X, Y) ), bel( on(X, Y) ) then drop( target(X, Y) ).
	
	% configuration has changed, check whether there is a reason to drop the target.
	% first reason: if block now is in position, then drop any targets for block.
	if goal( target(X,Y) ), inPosition(X) then drop( target(X,Y) ).
	% second reason: target block to be moved can no longer be moved (block cannot be picked up).
	if goal( target(X,Y) ), bel( (not(clear(X)), not(holding(X))) ; (holding(Z), not(Z=X))  ) then drop( target(X,Y) ).
	% third reason: target tower no longer exists, and planned move is no longer constructive.
	if goal( target(X,Y), not(Y = table) ), a-goal( tower([X,Y|T]) ), not(bel( tower([Y|T]) )) then drop( target(X,Y) ).
	% fourth reason: target location is no longer clear.
	if goal( target(X,Y) ), bel( not(clear(Y)) ) then drop( target(X,Y) ).
	% fifth reason: constructive move has become available for targeted block.
	if goal( target(X,table) ), constructiveMove(X,Y) then drop( target(X,table) ) + adopt( target(X,Y) ).
	
	% PART 2: ...and then act.
	
	% perform action in environment.
	if goal( target(X,Y) ) then pickup(X).
	if goal( target(X,Y)) then putdown(X,Y).

	% otherwise, there is nothing to do, so we can move the gripper to the top left corner.
	if true then nil.
}
